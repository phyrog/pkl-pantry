open module brine

import "@k8s/K8sResource.pkl"
import "@k8s/apiextensions-apiserver/pkg/apis/apiextensions/v1/CustomResourceDefinition.pkl"

configMaps = resourceMapping(import("@k8s/api/core/v1/ConfigMap.pkl"))
namespaces = resourceMapping(import("@k8s/api/core/v1/Namespace.pkl"))
persistentVolumes = resourceMapping(import("@k8s/api/core/v1/PersistentVolume.pkl"))
persistentVolumeClaims = resourceMapping(import("@k8s/api/core/v1/PersistentVolumeClaim.pkl"))
pods = resourceMapping(import("@k8s/api/core/v1/Pod.pkl"))
secrets = resourceMapping(import("@k8s/api/core/v1/Secret.pkl"))
services = resourceMapping(import("@k8s/api/core/v1/Service.pkl"))
serviceAccounts = resourceMapping(import("@k8s/api/core/v1/ServiceAccount.pkl"))

daemonSets = resourceMapping(import("@k8s/api/apps/v1/DaemonSet.pkl"))
deployments = resourceMapping(import("@k8s/api/apps/v1/Deployment.pkl"))
statefulSets = resourceMapping(import("@k8s/api/apps/v1/StatefulSet.pkl"))

crds = crdMapping()

function resourceMapping(type): Mapping<String, unknown> =
  new Mapping { default = (key) -> (type) { metadata { name = key } } }

function crdMapping(): Mapping<String, CustomResourceDefinition> = new Mapping {
  default = (key) -> (CustomResourceDefinition) { 
    local n = key.split(".")
    metadata { name = key }
    spec {
      group = n.drop(1).join(".")
      names {
        plural = n[0]
      }
    }
  }
}

output {
  value = module.toMap().values.flatMap((it) -> it.toMap().values.filterNonNull())
  renderer = (K8sResource.output.renderer as YamlRenderer) {
    isStream = true
  }
}
